{
  parserClass="org.intellij.sdk.language.parser.TranslationsParser"
  parserUtilClass="org.intellij.sdk.language.parser.TranslationsParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Translations"
  psiImplClassSuffix="Impl"
  psiPackage="org.intellij.sdk.language.psi"
  psiImplPackage="org.intellij.sdk.language.psi.impl"

  elementTypeHolderClass="org.intellij.sdk.language.psi.TranslationsTypes"
  elementTypeClass="org.intellij.sdk.language.psi.TranslationsElementType"
  tokenTypeClass="org.intellij.sdk.language.psi.TranslationsTokenType"
}

root ::= contents <<eof>>

contents ::= content*
content ::= content_tag | self_closing_tag | placeholder | text_element | choice_placeholder

content_tag ::= open_tag contents close_tag? {extends=content}
open_tag ::= TAG_OPEN key attributes TAG_CLOSE
close_tag ::= TAG_OPEN TAG_END key TAG_CLOSE

self_closing_tag ::= TAG_OPEN key attributes TAG_END TAG_CLOSE {extends=content}
text_element ::= (MISC | WHITESPACE)+ {extends=content}
placeholder ::= PH_OPEN key attributes PH_CLOSE {extends=content}
choice_placeholder ::= PH_OPEN key attributes CHOICE choice_option (SEPARATOR choice_option)* PH_CLOSE
choice_option ::= MISC
key ::= LITERAL

attributes ::= (SEPARATOR attribute)*
attribute ::= NUMBER | LITERAL | (SQUOTE MISC* SQUOTE) | (DQUOTE MISC* DQUOTE)
private ws ::= WHITESPACE?
addall ::= ESCAPE CHOICE WHITESPACE