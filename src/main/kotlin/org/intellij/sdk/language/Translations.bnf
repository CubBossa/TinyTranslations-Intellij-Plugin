{
  parserClass="org.intellij.sdk.language.parser.TranslationsParser"
  parserUtilClass="org.intellij.sdk.language.parser.TranslationsParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Translations"
  psiImplClassSuffix="Impl"
  psiPackage="org.intellij.sdk.language.psi"
  psiImplPackage="org.intellij.sdk.language.psi.impl"

  elementTypeHolderClass="org.intellij.sdk.language.psi.TranslationsTypes"
  elementTypeClass="org.intellij.sdk.language.psi.TranslationsElementType"
  tokenTypeClass="org.intellij.sdk.language.psi.TranslationsTokenType"
}

root ::= contents <<eof>>

contents ::= content*
content ::= content_tag | self_closing_tag | placeholder | text_element

content_tag ::= open_tag contents close_tag? {extends=content}
open_tag ::= TAG_OPEN open_tag_key attributes TAG_CLOSE
open_tag_key ::= <<open>>
close_tag ::= TAG_OPEN TAG_END close_tag_key TAG_CLOSE
close_tag_key ::= <<close>>

self_closing_tag ::= TAG_OPEN TAG_KEY attributes TAG_END TAG_CLOSE {extends=content}
text_element ::= MISC+ {extends=content}
placeholder ::= PH_OPEN PH_KEY attributes PH_CLOSE {extends=content}

private attributes ::= (SEPARATOR attribute)*
private attribute ::= BOOL | NUMBER | VALUE